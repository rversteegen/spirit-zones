plotscript, falling star, begin
    #init draw pill bar()
    # music is paying on cutscene wth
    suspend map music
    intro cutscene()  # intro/introcut.hss
    fade screen out
    set hero picture(0, 7, outside battle)
    suspend player
    set NPC obstructs (7, false)
    set NPC ignores walls (7, true)
    camera follows NPC (7)
    wait (100)
    fade screen in
    play song (5)
    #play sound (19, true)
    walk NPC to X (7, 3)
    walk NPC to Y (7, 48)
    wait for NPC (7)
    #stop sound (19)
    # whiteout begins
    fade screen out (63, 63, 63)
    play sound (18)
    stop song ()
    destroy NPC (7)
    reset hero picture ()
    write map block (3, 48, 133, 1)
    write map block (3, 49, 134, 1)
    wait (20)
    fade screen in
    #whiteout ends
    camera follows hero (0)
    resume map music
    play song (get ambient music)

    # FIXME: remove this it's just for ui...
    init draw pill bar ()

    # The rest of this is just for the spirit helper

    # a symbol/exclaimation mark pops over the spirit helper's
    # head
    # TODO: maybe make blink or animate?
    variable (notice mark sprite)
    notice mark sprite := load walkabout sprite (159)
    put slice (notice mark sprite, 100, 120)
    wait (10)
    # should probably play sound
    free sprite (notice mark sprite)

    # spirit jumps
    set npc z (18, 4)
    wait (1)
    set npc z (18, 8)
    wait (1)
    set npc z (18, 0)
    wait (1)

    # the spirit helper walks over to player, looks
    # at them
    walk NPC to X (18, 3)
    wait for NPC (18)
    set NPC direction(18, north)

    # .. then they say hi and ask ur name
    show text box (47)
    wait for text box
    rename hero (0)

    # "in this year of xxxx..."
    $1 = ""
    variable(the current year)
    the current year := system year ()
    append number(1, the current year)
    show text box (64)
    wait for text box

    # Don't worry! Afterworld is better.
    show text box (65)
    wait for text box

    # Me? I'm here to...
    show text box (136)
    wait for text box

    # Where's your wife? Warp God would know...
    show text box (135)
    wait for text box

    # move them to be by the god
    walk NPC to X (18, 13)
    wait for NPC (18)
    walk NPC to Y (18, 49)
    wait for NPC (18)
    walk NPC to X (18, 16)
    wait for NPC (18)
    set npc position (18, 31, 50)
    set npc direction(18, south)
    wait for NPC (18)

    # place uneasy anime lines over character, as well
    # as a "..." balloon
    variable (ellipsis)
    ellipsis := load walkabout sprite (164)
    put slice (ellipsis, 60, 100)

    variable (lines)
    lines := load walkabout sprite (163)
    put slice (lines, 60, 120)

    variable (counter)
    for (counter, 1, 4, 1) do (
        set sprite frame (ellipsis, counter)
        set sprite frame (lines, counter)
        wait (10)
    )

    free sprite (lines)
    free sprite (ellipsis)

    # end
    resume player
end
