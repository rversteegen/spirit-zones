# Sunset Fake Parallax
# By SlimeMaid
# Thanks to Bob the Hamster for help!
#
# Simple effect where a sunset stays in a fixed position, and a seagull moves
# at a slow rate across the screen as you go left/right (parallax).

# Reserve two constants for two different timers, one for the timer
# which updates the position of the sun backdrop screen and the seagull's
# position, and another which simply alternates two frames of animation for
# the sun backdrop and the seagull.
define constant(3, update positions timer)
define constant(5, animations timer)

# The current frame/screen # of the fixed sunset backdrop
# (the reflection is animated).
global variable (500, sunset backdrop current)

# FIXME: description comment
global variable (503, seagull walkabout)

# FIXME: comment!
global variable (504, map background layer)

# Prepares the sunset backdrop/seagull parallax and start the timers!
plotscript, load start parallax, begin
    # FIXME: comment...
    # Load the sunset and seagull sprites and...
    sunset backdrop current := load backdrop sprite (29)
    seagull walkabout := load walkabout sprite (115)
    set parent(seagull walkabout, sunset backdrop current)

    # Do a lot of magic so the sunset and seagul both render
    # below the tilemap itself.
    map background layer := lookup slice(sl:map layer 0)
    move slice below(sunset backdrop current, lookup slice(sl:map root))
    variable(background rectangle)
    # A background rectangle is created because otherwise we get an
    # awesome nodraw effect...
    background rectangle :=  createrect(320, 200)
    set rect border(background rectangle, border:none)
    move slice below(background rectangle, sunset backdrop current)
    set slice visible(lookup slice(sl:map layer 0), false)

    #set parent(sunset backdrop current, map background layer)
    #set parent(sunset backdrop current, get hero slice (0))

    # Start the timers (which call themselves)
    # NOTE/FIXME: I hope timers don't keep calling themselves even after
    # the map has ended! :p I think that's the case...
    set timer(update positions timer, 0, 1, @update positions)
    set timer(animations timer, 0, 10, @animations)
end


# Changes the sunset and seagull sprite to appear animated;
# both the sunset's reflection and the seagul have two frames
# of animation.
script, animations, begin
    if (get sprite frame(seagull walkabout) == 1) then (
        set sprite frame(seagull walkabout, 2)
        replace backdrop sprite(sunset backdrop current, 29)
    ) else (
        set sprite frame(seagull walkabout, 1)
        replace backdrop sprite(sunset backdrop current, 30)
    )

    set timer(animations timer, 0, 10, @animations)
end


# Update the seagul and sunset positions on screen so that
# the sunset is always in the same place (fixed) and the seagul
# slowly moves across the screen (parallax-like effect).
script, update positions, begin
    # both seagull and sunset must be set every tick
    put slice screen (sunset backdrop current, 0, 0)
    # do some math to set x position...
    variable (seagull position x)
    seagull position x := (camera pixel x / 20)
    put slice screen (seagull walkabout, seagull position x, camera pixel y + 14)
    set timer(update positions timer, 0, 1, @update positions)
end
